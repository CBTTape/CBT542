/* REXX - #DELNDUP : EDIT Macro to remove non duplicate lines         */
/**********************************************************************/
/* Written - A.J.GRAY : 16/09/1998                                    */
/**********************************************************************/
/* The following test that this is a valid ISREDIT environment.       */
/**********************************************************************/
options = ''                           /*                             */
Address ISPEXEC                        /*                             */
"IsrEdit Macro"                        /* TEST ISREDIT                */
mrc = rc                               /*                             */
zerralrm = 'NO'                        /* SET MESSAGE ALARM TO NO     */
zerrhm   = 'ISR00003'                  /* SET HELP SCREEN TO DEFAULT  */
x = Msg(off)                           /*  DISABLE MESSAGES           */
x = Prompt(off)                        /*  DISABLE PROMPTING          */
/**********************************************************************/
If (mrc > 0) Then                      /* TEST IF EDIT ENVIRONMENT    */
 Do                                    /*                             */
  zedsmsg = 'Edit Macro only'          /*                             */
  zedlmsg = '"#DELNDUP" can only be run as an Edit Macro' /*          */
  "SetMsg Msg(ISRZ001)"                /*                             */
  Signal FINISH                        /*                             */
 End                                   /*                             */
/**********************************************************************/
Say "Deleting non-duplicate lines"     /*                             */
oldline = 1                            /*                             */
newline = 2                            /*                             */
Do Forever                             /*                             */
 Address ISPEXEC "Control Errors Return" /*                           */
 "IsrEdit (line1) = Line "oldline      /*                             */
 "IsrEdit (line2) = Line "newline      /*                             */
 If (rc > 0) Then Leave                /*                             */
 Address ISPEXEC "Control Errors Cancel" /*                           */
 If (line1 = line2) Then               /*                             */
  Do                                   /*                             */
   flag.oldline = 'DUP'                /*                             */
   flag.newline = 'DUP'                /*                             */
  End                                  /*                             */
 newline = newline + 1                 /*                             */
 oldline = oldline + 1                 /*                             */
End                                    /*                             */
nondup = 0                             /*                             */
Do loop = oldline to 1 by -1           /*                             */
 If (flag.loop /= 'DUP') Then          /*                             */
  Do                                   /*                             */
   "IsrEdit Delete "loop               /*                             */
   nondup = nondup + 1
  End                                  /*                             */
End                                    /*                             */
Say nondup" non-duplicate lines deleted" /*                           */
Exit 0                                 /*                             */
