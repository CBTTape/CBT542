/* REXX - DDSCAN : Scan a selected DDNAME in JCL for a member         */
/**********************************************************************/
/* Written - A.J.Gray - 22/01/1999                                    */
/*         - A.J.Gray - 09/08/2002 - added to CBT                     */
/**********************************************************************/
/* Invoke with "DDSCAN memname" on the command line and then place    */
/* the cursor on the DDNAME to be searched.                           */
/*                                                                    */
/* This exec does the easy bit by extracting the dataset names and    */
/* allocating to ddname DDSCAN before hading over to the ISRFIND      */
/* utility for the 'legwork'.                                         */
/**********************************************************************/
/* The Following test that this is a valid ISREDIT environment.       */
/**********************************************************************/
options = ''                           /*                             */
Address ISPEXEC                        /*                             */
"ISREDIT Macro (scan)"                 /* Test ISREDIT                */
mrc = rc                               /*                             */
zerralrm = 'NO'                        /* set message alarm to no     */
zerrhm   = 'ISR00003'                  /* set help screen to default  */
x = Msg(off)                           /*  disable messages           */
x = Prompt(off)                        /*  disable prompting          */
If (mrc > 0) Then                      /* test if edit environment    */
 Do                                    /*                             */
  ZedSmsg = 'Edit Macro Only'          /*                             */
  ZedLmsg = '"DDSCAN" can only be run as an edit macro' /*            */
  "SetMsg Msg(ISRZ001)"                /*                             */
  Signal FINISH                        /*                             */
 End                                   /*                             */
/**********************************************************************/
"IsrEdit (dsname) = Dataset"           /* get dataset name            */
"IsrEdit (memname) = Member"           /* get member name             */
"IsrEdit (top,bot) = Display_Lines"    /* get number of lines in dsn  */
"IsrEdit (Row,Col) = Cursor"           /* Get location of cursor      */
/**********************************************************************/
/* Allocate and read member being edited                              */
Address TSO "Alloc Fi(MEMIN) Da('"dsname"("memname")') Shr Reu"
Address TSO "ExecIO * DiskR MEMIN (Finis Stem line.)"
Address TSO "Free Fi(MEMIN)"
/**********************************************************************/
dcnt = 0                               /* set dataset counter         */
dsnlist = ""                           /* initialise dsnlist          */
Do loop = row to line.0                /* roop thru member lines      */
 If (Substr(line.loop,1,3) /= '//*') &, /*                            */
    (Substr(line.loop,1,3) /= '// ') &, /*                            */
    (loop > row) Then                  /*                             */
  Leave                                /*                             */
 If (Substr(line.loop,1,3) = '//*') Then /*                           */
  Iterate                              /*                             */
 dpos = Index(line.loop,"DSN=")        /*                             */
 If (dpos > 0) Then                    /*                             */
  Do                                   /*                             */
   line = Substr(line.loop,dpos+4)     /*                             */
   line = Translate(line,'',''',:;/)') /*                             */
   Parse Upper Var line dsn .          /*                             */
   If (Sysdsn("'"dsn"'") <> 'OK') Then /*                             */
    Say "'"dsn"' not in catalog, not scanning." /*                    */
   Else                                /*                             */
    Do                                 /*                             */
     dcnt = dcnt + 1                   /*                             */
     dsn.dcnt = dsn                    /*                             */
     dsnlist = dsnlist||" '"||dsn||"'" /*                             */
    End                                /*                             */
  End                                  /*                             */
End                                    /*                             */
/**********************************************************************/
Address TSO                            /*                             */
"Alloc Fi(DDSCAN) Da("dsnlist") SHR REU" /*                           */
"ISRFIND DD(DDSCAN) M("scan") ISPF"    /*                             */
"Free Fi(DDSCAN)"                      /*                             */
/**********************************************************************/
FINISH:                                /*                             */
Exit 0                                 /* Exit exec                   */
