/* REXX - TABLSTAT : Add ISPF stats to all ISPF tables in a PDS       */
/**********************************************************************/
/* Author : Alastair Gray - a_j_gray@hotmail.com                      */
/*                 (or the e-mail I use to post on TSO-REXX/IBM-MAIN) */
/**********************************************************************/
/* Changes :                                                          */
/* 1.0.0   - 18/09/1998 - Alastair Gray - Original version            */
/* 1.0.1   - 09/06/2002 - Alastair Gray                               */
/* 1.0.2   - 13/09/2002 - Alastair Gray - Added comments              */
/**********************************************************************/
Arg dsn                                /*                             */
dsn = Strip(dsn,B,"'")                 /*                             */
x = Msg(on)                            /*                             */
x = Prompt(off)                        /*                             */
/**********************************************************************/
Address ISPEXEC                        /*                             */
"Control Errors Return"                /*                             */
"LibDef  TABSTAT Dataset Id('"dsn"')"  /*                             */
"LMInit  DataID(table) Dataset('"dsn"') Enq(ShrW)" /*                 */
"LMOpen  DataID("table") Option(Input)"/*                             */
member = ''                            /*                             */
lrc = 0                                /* loop on members             */
Do while lrc = 0                       /*                             */
 "LMMList DataID("table") Option(List) Member(member) Stats(yes)"
 lrc = rc                              /*                             */
 member = Strip(member,T)              /* get member name             */
 If (zlvers = '') Then                 /* no version ?                */
  Do                                   /*  get table stats            */
   "TBStats "member" CDate(zlcdate) UDate(zlmdate) Utime(zlmtime) ",
            "User(zluser) Library(TABSTAT) ",
            "Status1(stat1) Status2(stat2) Status3(stat3)"
   trc = rc                            /*                             */
   zlvers = 1                          /*                             */
   zlmod  = 0                          /*                             */
   If (stat2 > 1) |,                   /* if 'old' table              */
      (zlcdate = 'R1 TABLE') Then      /*  go to read direct          */
    Do                                 /*                             */
     Address TSO                       /*                             */
     "Alloc Fi(MEMIN) Da('"dsn"("member")') Shr Reu" /*               */
     "ExecIO 1 DiskR MEMIN (Finis Stem line.)" /*                     */
     "Free Fi(MEMIN)"                  /*                             */
     Address ISPEXEC                   /*                             */
     zluser = Substr(line.1,51,8)      /*  get userid                 */
     zlcdate = Substr(line.1,39,3)     /*  get creation date          */
     zlmdate = Substr(line.1,45,3)     /*  get modification date      */
     zlmdate = Substr(C2x(zlmdate),1,5)/*                             */
     zlmdate = Date('O',zlmdate,'J')   /*  convert date format        */
     If (zlcdate = '   ') Then         /*  no creation                */
      zlcdate = zlmdate                /*   use modification          */
     Else                              /*                             */
      Do                               /*                             */
       zlcdate = Substr(C2x(zlcdate),1,5) /*                          */
       zlcdate = Date('O',zlcdate,'J') /*  convert date format        */
      End                              /*                             */
     zlmtime = Substr(line.1,48,3)     /*  get modification time      */
     zlmtime = C2x(zlmtime)            /*                             */
     zlmtime = Substr(zlmtime,1,2)||':'||Substr(zlmtime,3,2)
    End                                /*                             */
   Else                                /* Tbstats OK                  */
    If (zlcdate /= '') Then            /*                             */
     Do                                /*                             */
      zlmtime = Substr(zlmtime,1,2)||":"||Substr(zlmtime,4,2)
      zluser = Strip(zluser,T)         /*                             */
     End                               /*                             */
   "LMMStats DataID("table") Member("member") ",
            "Version("zlvers") ModLevel("zlmod") ",
            "Created("zlcdate") ",     /*                             */
            "ModDate("zlmdate") ModTime("zlmtime") ",
            "User("zluser") "          /*                             */
   src = rc                            /*                             */
  End                                  /*                             */
End                                    /*                             */
"LMMList DataID("table") Option(Free)" /*                             */
"LMClose DataID("table")"              /*                             */
"LMFree  DataID("tabls")"              /*                             */
"LibDef  TABSTAT"                      /*                             */
/**********************************************************************/
Exit 0                                 /*                             */
