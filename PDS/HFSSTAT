/* REXX - HFSSTAT  : Get size/space stats for an HFS file             */
/**********************************************************************/
/* Written - A.J.Gray : 04/02/2000 - only works if cyls/trks          */
/*                                   requires sufficient workspace    */
/*                      11/02/2002 - Comment s/lightly for CBT        */
/**********************************************************************/
Trace O                              /* Trace Off                     */
Arg dsn                              /* get dsname                    */
dsn = Strip(dsn,B,"'")               /* clean it up                   */
/**********************************************************************/
Address ISPEXEC                      /*                               */
dsname = ''                          /*                               */
"LmdInit ListID(LID) Level("dsn")"   /* initialise for detail         */
"LmdList ListID("lid") Dataset(DSNAME) Option(LIST) Stats(YES)"
"LmdFree ListID("lid")"              /* free i tup                    */
If (zdldsorg /= 'HFS') Then          /* check if HFS                  */
 Do                                  /*                               */
  Say "Not an HFS file"              /*  not an HFS file              */
  Signal FINISH                      /*                               */
 End                                 /*                               */
temp = "T"Right(Time(L),6)           /* build a temp dataset name     */
Address TSO                          /*                               */
"Alloc Fi("temp") Da('"userid()"."temp"') ",
                  "New Reu Delete Like('"dsn"') Unit(3390) ",
                  "Dsorg(PS) LRECL(80) Recfm(F B) Blksize(80)"
x = ListDSI(temp FILE)               /* get temp file details         */
"Free  Fi("temp")"                   /*                               */
If (sysunits = 'CYLINDER') Then factor = 15 /* check if cylinders     */
Else factor = 1                      /* otherwise tracks              */
exspace  = (zdlext - 1) * (sysseconds * factor) /* get extent detail  */
calcprim = (zdlsize - exspace) / factor /* get primary detail         */
zdlsize  = zdlsize / factor          /* get size                      */
Say "Volume     = "zdlvol            /*                               */
Say "Device     = "zdldev            /*                               */
Say "Size       = "Left(Strip(zdlsize,B),8)" "zdlspacu
Say "Primary    = "Left(calcprim,8)" "zdlspacu
Say "Secondary  = "Left(sysseconds,8)" "zdlspacu
Say "%used      = "Strip(zdlused,B)  /*                               */
Say "Extents    = "Strip(zdlext,B)   /*                               */
Say "Creation   = "zdlcdate          /*                               */
Say "Expiration = "zdledate          /*                               */
Say "Referenced = "zdlrdate          /*                               */
/**********************************************************************/
FINISH:                              /*                               */
Exit 0                               /*                               */
